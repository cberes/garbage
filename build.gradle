plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

group = projectGroup
version = projectVersion
ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    testImplementation 'org.hamcrest:hamcrest:2.1'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        csv.enabled = false
        html.enabled = true
        xml.enabled = true // coveralls plugin depends on xml format report
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'garbage'
                packaging 'jar'
                description 'Is it garbage day?'
                url 'https://github.com/cberes/garbage'

                scm {
                    url 'https://github.com/cberes/garbage.git'
                }

                licenses {
                    license {
                        name 'GNU General Public License v3.0'
                        url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'cberes'
                        name 'Corey Beres'
                        email 'corey.beres@gmail.com'
                    }
                }
            }
        }
    }
}
